load("//tools/dev-server:index.bzl", "dev_server")
load("//tools:defaults.bzl", "ng_module", "sass_binary")
load("//tools/esbuild:index.bzl", "esbuild", "esbuild_config")
load("//src/gngt-examples:config.bzl", "ALL_EXAMPLES")
load("//tools/angular:index.bzl", "LINKER_PROCESSED_FW_PACKAGES")

package(default_visibility = ["//visibility:public"])

# List of dependencies that are referenced in the `index.html` file.
devserverIndexHtmlDependencies = [
    "@npm//zone.js",
    "@npm//core-js-bundle",
    "@npm//@angular/material",
    "@npm//@ionic/angular",
    ":index.html",
]

exports_files([
    "devserver-configure.js",
    "protractor.conf.js",
    "start-devserver.js",
])

ng_module(
    name = "e2e-app",
    testonly = True,
    srcs = glob(
        ["**/*.ts"],
        exclude = ["test-util/**"],
    ),
    assets = glob(
        [
            "**/*.html",
            "**/*.css",
        ],
        exclude = ["index.html"],
    ),
    deps = [
        "//src/gngt-examples/private",
        "//src/core/translations",
        "//src/ionic/calendar",
        "//src/material/calendar",
        "@npm//@angular/common",
        "@npm//@angular/core",
        "@npm//@angular/forms",
        "@npm//@angular/platform-browser",
        "@npm//@angular/router",
        "@npm//@ionic/angular",
        "@npm//@ngneat/transloco",
        "@npm//@ngx-translate/http-loader",
    ],
)

esbuild_config(
    name = "esbuild_config",
    config_file = "esbuild.config.mjs",
    deps = [
        "//tools/esbuild:custom_resolve_esbuild_plugin",
    ],
)

esbuild(
    name = "bundles",
    testonly = True,
    config = ":esbuild_config",
    entry_points = [":main.ts"] + ["%s:index.ts" % e for e in ALL_EXAMPLES],
    platform = "browser",
    splitting = True,
    # We cannot use `ES2017` or higher as that would result in `async/await` not being downleveled.
    # ZoneJS needs to be able to intercept these as otherwise change detection would not work properly.
    target = "es2016",
    # Note: We add all linker-processed FW packages as dependencies here so that ESBuild will
    # map all framework packages to their linker-processed bundles from `tools/angular`.
    deps = LINKER_PROCESSED_FW_PACKAGES + [
        ":e2e-app",
    ],
)

dev_server(
    name = "devserver",
    testonly = True,
    srcs = devserverIndexHtmlDependencies,
    additional_root_paths = [
        "npm/node_modules",
    ],
    tags = ["manual"],
    deps = [
        ":bundles",
    ],
)
