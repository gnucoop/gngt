package(default_visibility = ["//visibility:public"])

load("//tools:defaults.bzl", "ng_module", "ng_package")
load("//tools/highlight-files:index.bzl", "highlight_files")
load("//tools/package-docs-content:index.bzl", "package_docs_content")

EXAMPLE_PACKAGES = [
    # TODO(devversion): try to have for each entry-point a bazel package so that
    # we can automate this using the "package.bzl" variables. Currently generated
    # with "bazel query 'kind("ng_module", //src/material-examples/...:*)' --output="label"
    "//src/material-examples/material/calendar",
]

ng_module(
    name = "material-examples",
    srcs = glob(["*.ts"]) + [":example-module.ts"],
    module_name = "@gngt/material-examples",
    deps = EXAMPLE_PACKAGES,
)

filegroup(
    name = "example-source-files",
    srcs = ["%s:source-files" % pkg for pkg in EXAMPLE_PACKAGES],
)

highlight_files(
    name = "highlighted-source-files",
    srcs = [":example-source-files"],
    tags = ["docs-package"],
)

package_docs_content(
    name = "docs-content",
    srcs = {
        # We want to package the guides in to the docs content. These will be displayed
        # in the documentation.
        "//guides": "guides",

        # Package the overviews for "@gngt/material" and "@gngt/core" into the docs content
        "//src/core:overviews": "overviews/core",
        "//src/material:overviews": "overviews/material",

        # Package the API docs for the Material and Core package into the docs-content
        "//src:api-docs": "api-docs",

        # In order to be able to run examples in StackBlitz, we also want to package the
        # plain source files into the docs-content.
        ":example-source-files": "examples-source",

        # For the live-examples in our docs, we want to package the highlighted files
        # into the docs content. These will be used to show the source code for examples.
        ":highlighted-source-files": "examples-highlighted",
    },
    tags = ["docs-package"],
)

ng_package(
    name = "npm_package",
    srcs = ["package.json"],
    data = [":docs-content"],
    entry_point = ":public-api.ts",
    tags = ["docs-package"],
    deps = [":material-examples"] + EXAMPLE_PACKAGES,
)

genrule(
    name = "example-module",
    srcs = [":example-source-files"],
    outs = [
        "example-module.ts",
        "_example_module.MF",
    ],
    cmd = """
      # Create source file manifest
      echo "$(locations //src/material-examples:example-source-files)" \
          > $(location _example_module.MF)

      # Run the bazel entry-point for generating the example module.
      ./$(location //tools/example-module:bazel-bin) \
          "$(location _example_module.MF)" \
          "$(location example-module.ts)" \
          "$$PWD/src/material-examples"
    """,
    output_to_bindir = True,
    tools = ["//tools/example-module:bazel-bin"],
)
